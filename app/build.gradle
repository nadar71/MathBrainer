apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-parcelize'



configurations {
    ktlint
}

android {
    compileSdkVersion 34
    defaultConfig {
        applicationId "eu.indiewalkabout.mathbrainer"
        minSdkVersion 19
        targetSdkVersion 34
        versionCode 3
        versionName "2.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        // signingConfig signingConfigs.release
        multiDexEnabled true
    }

    buildTypes {
        release {
            zipAlignEnabled true
            jniDebuggable false
            renderscriptDebuggable false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            ndk {
                debugSymbolLevel 'FULL'
            }
        }
        debug {
            debuggable true
            minifyEnabled false
        }
    }

    flavorDimensions "MathBrainer"
    productFlavors {
        production {
            dimension "MathBrainer"
            resValue "string", "app_name", "MathBrainer"
        }

        myTesting {
            dimension "MathBrainer"
            resValue "string", "app_name", "MathBrainer Test"
            applicationIdSuffix ".testing"
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            unitTests.returnDefaultValues = true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11
    }

    lintOptions {
        abortOnError false
    }

    dataBinding {
        enabled true
    }

    viewBinding {
        enabled true
    }



}


dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "androidx.appcompat:appcompat:$appcompat_vers"
    implementation "androidx.constraintlayout:constraintlayout:$constraintlayout_vers"

    implementation "com.google.android.gms:play-services-ads:$play_services_ads_vers"

    // GDPR
    implementation "com.google.android.ads.consent:consent-library:$consent_library_vers"

    // https://ktlint.github.io/#getting-started
    ktlint 'com.pinterest:ktlint:0.39.0'

    // LiveData dependencies
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_vers"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_vers"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_ext_vers"

    // Room dependencies
    implementation "androidx.room:room-runtime:$room_vers"
    kapt "androidx.room:room-compiler:$room_vers"
    implementation "androidx.room:room-ktx:$room_vers"
    // test helpers
    testImplementation "androidx.room:room-testing:$room_vers"

    // unity ads
    // compile (name:'unity-ads', ext: 'aar')
    implementation project(':unity-ads')

    // kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_vers"


    // Testing code should not be included in the main code.
    // Once https://issuetracker.google.com/128612536 is fixed this can be fixed.
    implementation "androidx.test:core:1.5.0"

    //  AndroidX Test - local unit tests - JVM testing
    testImplementation "junit:junit:4.13.2"
    testImplementation "org.hamcrest:hamcrest-all:1.3"
    testImplementation "androidx.arch.core:core-testing:2.2.0"
    testImplementation 'androidx.test:runner:1.5.2'
    testImplementation "androidx.test:core-ktx:1.5.0"
    testImplementation "androidx.test.ext:junit:1.1.5"
    testImplementation "org.robolectric:robolectric:4.3.1"
    testImplementation 'org.mockito:mockito-core:2.25.0'
    testImplementation 'org.mockito:mockito-inline:2.13.0'
    testImplementation "io.mockk:mockk:1.9.3"


    // AndroidX Test - Instrumented testing
    androidTestImplementation "junit:junit:4.13.2"
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation "androidx.test.ext:junit:1.1.5"
    androidTestImplementation "androidx.arch.core:core-testing:2.2.0"
    androidTestImplementation "androidx.test:rules:1.5.0"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.6.0-alpha01"
    androidTestImplementation "androidx.test.espresso:espresso-intents:3.6.0-alpha01"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:3.6.0-alpha01"


}


task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    main = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"
    // to generate report in checkstyle format prepend following args:
    // "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/ktlint.xml"
    // see https://github.com/pinterest/ktlint#usage for more
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    main = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "src/**/*.kt"
}
